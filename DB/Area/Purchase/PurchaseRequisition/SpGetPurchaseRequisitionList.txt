  
-- =============================================  
-- Author:  ajith@kraftlabs.com  
-- Create date: 11-May-2019  
-- Description: Get List of Purchase Requisition for datatable result  
-- =============================================  
--exec Purchase.SpGetPurchaseRequisitionList 'draft', '','','','','','','TransNo','ASC',0,20,2018,1,1  
ALTER PROCEDURE [Purchase].[SpGetPurchaseRequisitionList](  
 @Type varchar(20),  
 @TransNoHint varchar(20),  
 @TransDateHint varchar(20),  
 @FromDepartmentHint varchar(100),  
 @ToDepartmentHint varchar(100),  
 @CategoryNameHint varchar(100),  
 @ItemNameHint varchar(100),  
 @SortField varchar(100),  
 @SortOrder varchar(10),  
 @Offset int = 0,  
 @Limit int = 20,  
 @UserID int,  
 @FinYear int,  
 @LocationID int,  
 @ApplicationID int  
)  
AS  
BEGIN  
 SET NOCOUNT ON;  
 SET @Offset = @Offset + 1  
    DECLARE @IsViewer int,@LoginDeparmentID int,@DepartmentID int;  
  DECLARE @totalRecords int = 0;  
 SELECT @LoginDeparmentID=DepartmentID from Employee WHERE UserID=@UserID  
 SELECT @IsViewer = CASE WHEN COUNT(*) = 0 THEN 0 ELSE 1 END    
 FROM AspNetUsers U   
 LEFT JOIN UserRoles UR ON U.ID = UR.UserID  
 LEFT JOIN Roles R ON R.ID = UR.RoleID    
 WHERE  ((R.Name LIKE 'TRANSACTION VIEWER'AND UR.LocationID = @LocationID) OR UserName LIKE 'admin')  
 AND U.ID = @UserID  
 SELECT @DepartmentID = DepartmentID FROM Employee WHERE UserID = @UserID  
  
 SELECT @totalRecords =  COUNT(*)   
 FROM viPurchaseRequisition P  
 WHERE P.[Status] = @Type  
   AND (@TransNoHint IS NULL OR TransNo LIKE '%' + @TransNoHint+ '%'   )
  AND (@TransDateHint IS NULL OR TransDate LIKE '%' +@TransDateHint+ '%'   )
  AND (@FromDepartmentHint IS NULL OR FromDepartment LIKE '%' +@FromDepartmentHint+ '%'   )
  AND (@ToDepartmentHint IS NULL OR ToDepartment LIKE '%' +@ToDepartmentHint+ '%'   )
  AND (@CategoryNameHint IS NULL OR CategoryName LIKE '%' + @CategoryNameHint + '%'    )
  AND (@ItemNameHint IS NULL OR ItemName LIKE '%' + @ItemNameHint + '%'  )  
 AND (@IsViewer = 1   
 OR @UserID in (select UserID FROM dbo.FnGetPrivilegedUsers(P.CreatedUserID,P.LocationID,p.ToDepartmentID,p.FromDepartmentID,p.LocationID)))   
 AND LocationID = @LocationID   
 AND ApplicationID = @ApplicationID  
  
     ;WITH P_CTE AS (  
  SELECT top 10000  Count(*) OVER (Partition by 1) AS recordsFiltered, ID, TransNo, TransDate, FromDepartment, ToDepartment,   
  CategoryName, ItemName,  [Status], FromDepartmentID, ToDepartmentID, IsDraft, FullyOrdered, IsCancelled, CancelledDate,     
  CreatedUserID, CreatedDate, FinYear, LocationID,  
  ROW_NUMBER() OVER ( ORDER BY    
   CASE WHEN @SortField+@SortOrder = 'TransNoASC' THEN TransNo END ASC,  
   CASE WHEN @SortField+@SortOrder = 'TransNoDESC' THEN TransNo END DESC,  
     
   CASE WHEN @SortField+@SortOrder = 'TransDateASC' THEN TransDate END ASC,  
   CASE WHEN @SortField+@SortOrder = 'TransDateDESC' THEN TransDate END DESC,  
  
   CASE WHEN @SortField+@SortOrder = 'FromDepartmentASC' THEN FromDepartment END ASC,  
   CASE WHEN @SortField+@SortOrder = 'FromDepartmentDESC' THEN FromDepartment END DESC,  
  
   CASE WHEN @SortField+@SortOrder = 'ToDepartmentASC' THEN ToDepartment END ASC,  
   CASE WHEN @SortField+@SortOrder = 'ToDepartmentDESC' THEN ToDepartment END DESC,  
     
   CASE WHEN @SortField+@SortOrder = 'CategoryNameASC' THEN CategoryName END ASC,  
   CASE WHEN @SortField+@SortOrder = 'CategoryNameDESC' THEN CategoryName END DESC,  
     
   CASE WHEN @SortField+@SortOrder = 'ItemNameASC' THEN ItemName END ASC,  
   CASE WHEN @SortField+@SortOrder = 'ItemNameDESC' THEN ItemName END DESC  
   ) AS RowNum  
  FROM viPurchaseRequisition P   
  WHERE [Status] = @Type      
  AND (@TransNoHint IS NULL OR TransNo LIKE '%' + @TransNoHint+ '%'   )
  AND (@TransDateHint IS NULL OR TransDate LIKE '%' +@TransDateHint+ '%'   )
  AND (@FromDepartmentHint IS NULL OR FromDepartment LIKE '%' +@FromDepartmentHint+ '%'   )
  AND (@ToDepartmentHint IS NULL OR ToDepartment LIKE '%' +@ToDepartmentHint+ '%'   )
  AND (@CategoryNameHint IS NULL OR CategoryName LIKE '%' + @CategoryNameHint + '%'    )
  AND (@ItemNameHint IS NULL OR ItemName LIKE '%' + @ItemNameHint + '%'  )  
  AND (@IsViewer = 1   
   OR @UserID in (select UserID FROM dbo.FnGetPrivilegedUsers(P.CreatedUserID,P.LocationID,p.ToDepartmentID,p.FromDepartmentID,p.LocationID)))   
  AND LocationID = @LocationID   
     AND ApplicationID = @ApplicationID  
 )  
  
 SELECT ID, TransNo, TransDate,  FromDepartment, ToDepartment,   
  CategoryName, ItemName,  [Status], FromDepartmentID, ToDepartmentID, IsDraft, FullyOrdered, IsCancelled, CancelledDate,     
  CreatedUserID, CreatedDate, FinYear, LocationID, recordsFiltered, @totalRecords totalRecords  
 FROM P_CTE  
 WHERE RowNum >= @Offset  
 AND RowNum < @Offset + @Limit  
END  
      
  
  